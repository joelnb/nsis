# FIXME: install assembly and pascal includes into the correct locations

lib_target = "plugin"

lib_files = Split("""
	plugin.c
""")

api_files = Split("""
	plugin.h
	plugin.lib
	#Source/exehead/api.h
""")

example = Split("""
	exdll.c
	exdll.dpr
	exdll.dsp
	exdll.dsw
	exdll_with_unit.dpr
	nsis.pas
	extdll.inc
""")

Import('env plugin_env')

# build library

api_env = env.Clone()
api_env.Append(CPPPATH = ['#Source/exehead'])
lib = api_env.Library(lib_target, lib_files)

# distribute library, files and examples

env.DistributeExamples(example + api_files + lib, path='Plugin')

# make sure all the other plug-ins can use the library

plugin_env.Append(LIBPATH = [lib[0].dir])
plugin_env.Append(LIBS = [lib_target])
